#!/usr/bin/perl
use strict;
use Getopt::Std;
use Pod::Usage;
use Parse::MediaWikiDump;
use List::Util qw[min max];

binmode(STDOUT, ":utf8");

my %modules = ( 
  dk => { 
    name => "Datumskonventionen", 
    func => \&mod_datumsformat,
    show_info => 5,
    show_contextlines => 5
  },
  "dker" => { 
    name => "Datumskonventionen (er)", 
    func => \&mod_datumsformat,
    show_info => 5,
    show_contextlines => 5
  },
  "dkyy" => { 
    name => "Datumskonventionen (yy)", 
    func => \&mod_datumsformat,
    show_info => 5,
    show_contextlines => 5
  },
  "dkyyy" => { 
    name => "Datumskonventionen (yyy)", 
    func => \&mod_datumsformat,
    show_info => 5,
    show_contextlines => 5
  },
  bkl => { 
    name => "Begriffsklaerung", 
    func => \&mod_bkl,
    show_info => -1
  },
  red => { 
    name => "Redirects", 
    func => \&mod_redirects,
    redirects => 1, 
    show_info => -1,
    show_contextlines => -1
  },
  dagger => { 
    name => "Pluszeichen als Dagger", 
    func => \&mod_plusdagger,
    show_context => 10,
    show_contextlines => 5
  },
  'grep' => {
    name => "Grep",
    func => \&mod_simpleregexp,
    show_info => -1,
    show_contextlines => 5
  }
);

getopts("hc:l:i:d", \my %opts);

pod2usage(-verbose => 1) if $opts{h};
pod2usage unless @ARGV;

my $filename = shift @ARGV;

my $debug = defined $opts{d};

my @mods;

pod2usage("no module selected") if scalar @ARGV == 0;
for (@ARGV) {
  my $mod = $modules{$_};
  pod2usage("module $_ is unkown") if not defined $mod;
  push @mods, $mod;
  $mod->{show_context} ||= 30;
  $mod->{show_contextlines} ||= 0;
  $mod->{show_info} ||=0;
  $mod->{show_contextlines} = $opts{l} if defined $opts{l};
  $mod->{show_info} = $opts{i} if defined $opts{i};
  $mod->{shortname} = $_;
  $mod->{redirects} ||=0;
  $mod->{count} = 0; # found-counter
}

my $count = 0;

my %dk_filter_stats;
my $dk_filter_stats = 0;

#TODO: diese globalen Variablen entfernen
my $found;
my $context;
my $info;

my $secondary_flag = 0;
my $secondary_count = 0;

my $parser= Parse::MediaWikiDump::Pages->new($filename);

###########################################
# Konstanten
###########################################

my %monat_lang = (
  Januar => 1, "Jänner" => 1,
  Februar => 2,
  "März" => 3,
  April => 4,
  Mai => 5,
  Juni => 6,
  Juli => 7,
  August => 8,
  September => 9,
  Oktober => 10,
  November => 11,
  Dezember => 12);

my %monat;
while (my ($lang, $num) = each %monat_lang) {
  my $kurz = substr($lang,0,3);
  $monat{$lang} = $num;
  $monat{$kurz} = $num;
  $monat{$kurz."."} = $num;
}

###########################################
# Main
###########################################

my $page;

my $time_start = time;
my @time_hist;
my @byte_hist;
my $time_hist_length = 100;
for (my $i = 0; $i < $time_hist_length; $i++) {
  push @time_hist, $time_start;
  push @byte_hist, 0;
}
my $progress_step = 100; # in Artikeln

while(defined($page = $parser->next)) {
  $count++;

  my $title = $page->title;

  foreach my $mod (@mods) {
    ($found, $context, $info, $secondary_count) = ();

    $mod->{func}->($mod, $page) unless $page->redirect and !$mod->{redirects};
  
    if ($found > 0 ) {
      $mod->{count}++;
      output_one($mod, $title, $found == $secondary_count);
    }

  }
  
  print_progress() if ($count % $progress_step == 0);
  #last if $count > 5000;
}

print_progress();
print STDERR "\n";

###########################################
sub print_progress {
###########################################
  my $time_cur = time;
  my $byte_cur = $parser->current_byte;
  
  my $time_last = shift @time_hist;
  my $byte_last = shift @byte_hist;

  my $size = $parser->size;

  sub _print_rate {
    my ($text, $time_cur, $time_diff, $byte_cur, $byte_diff, $lemma_diff) = @_;
    my $rate = ($time_diff>0)?($byte_diff / ($time_diff)):0;
    my $lemma_rate = ($time_diff>0)?($lemma_diff / ($time_diff)):0;
    my $eta = ($rate>0)?(int (($size - $byte_cur) / $rate)):0;
    my $time_end = $eta + $time_cur - $time_start;
    printf STDERR ' %s: %d:%02d/%d:%02d (%.02f MB/s, %d A/s)', 
      $text,
      $eta / 60,
      $eta % 60,
      $time_end / 60,
      $time_end % 60,
      $rate / 1024 / 1024,
      $lemma_rate;
  }
 
  printf STDERR "\r".'%2.2f%% %d:%02d',
    $byte_cur/$size*100,
    ($time_cur-$time_start) / 60,
    ($time_cur-$time_start) % 60;
  _print_rate("total", $time_cur, $time_cur - $time_start,
              $byte_cur, $byte_cur, $count);
  _print_rate("cur", $time_cur, $time_cur - $time_last, 
              $byte_cur, $byte_cur - $byte_last,
              min($progress_step * $time_hist_length, $count));

  print STDERR " mods:";
  foreach my $mod (@mods) {
    printf STDERR " %s (%i)",
      $mod->{shortname},
      $mod->{count};
  }

  if ($debug and $dk_filter_stats > 0) {
    print STDERR " filter: $dk_filter_stats = ";
    print keys %dk_filter_stats;
    foreach my $filter (sort keys %dk_filter_stats) {
      printf STDERR " %s (%i)", $filter, $dk_filter_stats{$filter};
    }
  }

  push @time_hist, $time_cur;
  push @byte_hist, $byte_cur;
}

###########################################
# Modules
###########################################

###########################################
sub mod_simpleregexp {
###########################################
  my($mod, $page) = @_;
  return unless $page->namespace eq ''; # nur Artikel-Namensrausm
  my $title = $page->title;
  $_ = ${$page->text};
  if (/class="(prettytable|wikitable)"/i) {
    insert_found($mod, $page->title,$`,$&,$');
#    $_=$';
  }
}

###########################################
sub mod_selbstlinks {
###########################################
  my($page) = @_;
  my($title, $text) = ($page->title, ${$page->text});
  $_ = $text;
  while (/\[\[([^\]\|]+)(\||\]\])/) {
    if ($1 eq $title) {
      insert_found($title,$`,$&,$');
    }
    $_=$';
  }
}

###########################################
sub mod_bkl {
###########################################
  my($mod, $page) = @_;
  return unless $page->namespace eq ''; # nur Artikel-Namensrausm
  my $title = $page->title;
  $_ = ${$page->text};
  if (/\{\{[Bb]egriffsklärung\}\}/) {
    insert_simple($mod, $title ,"Position der Vorlage") if ($` =~ /^\s*$/);
    insert_simple($mod, $title, "Aufzaehlung") if (/^\s*#/m);
    insert_simple($mod, $title, "fehlende Einleitung") if (/^\s*\*/);
  }
  if (/\{\{BKL\}\}/) {
    insert_simple($mod, $title, "Falsche Vorlage");
  }
  if (/\[\[[KkCc]ategorie: *Begriffsklärung\]\]/) {
    insert_simple($mod, $title, "Kategorie");
  }
}

###########################################
sub mod_redirects {
###########################################
  my($mod, $page) = @_;
  return unless $page->redirect;
  my $title = $page->title;
  $_ = ${$page->text};
  #s/\#REDIRECT[ :]*\[\[.*?\]\]\.?//i; 
  s/\#REDIRECT.*$//mi; 
  my $context_hack = $_; #Hack
  s/\[\[.*?\]\]//g; # Links entfernen (inkl. Kategorien)
  s/\{\{.*?\}\}//sg; # Vorlagen entfernen
  s/\s//g; # Leerzeichen entfernen
  insert_simple($mod, $title ,"Text (" . (length $_) . ")") if length $_ > 0; 
  $context = $context_hack;
}

###########################################
sub mod_datumsformat {
###########################################
  my($mod, $page) = @_;
  return unless $page->namespace eq ''; # nur Artikel-Namensrausm
  
  $_ = ${$page->text};

  s/<!--.*?-->/<!-- -->/sg; # Kommentare leeren

  s/<span[^>]*display:none[^>]*>.*?<\/span>//sg; # display:none ignorieren

  s/\{\{ChartplatzierungenX?\}\}(.*?)<\/div>//sg; # chartboxen

  s/{{(DEFAULTSORT|SORTIERUNG):.*?}}//sg;

  # todo: 
  # Zitat-Vorlagen
  # "vermutlich http-link" überprüfen
  # Chartplatzierungen sieht komisch aus
  # Literatur/Weblinks/etc.-Abschnitte verbessern (auch "===")
  #   z.B. [[Alfred Philippson]]
  # {{Großes Bild}} 
  
  sub d {
    my ($bool, $text) = @_;
    $dk_filter_stats{$text}++ if !$bool;
    return $bool;
  }

  sub e {
    my $bool = d(@_);
    $secondary_flag = 1 if !$bool;
    return 1;
  }

  sub _check {
    my ($day, $month, $year, $before, $after) = @_;
    my ($a, $b) = ($after, $before);
    $dk_filter_stats++;
    return (
      d($month <= 12, "m<=12") and  # plausibles Datum 
      d($month >= 1, "m>=1") and  # plausibles Datum 
      d($day <= 31, "d<=31") and
      d($day >= 1, "d>=1") and
      d($year <= 2100, "y<=2100") and
      d($b !~ /https?:\/\/\S*$/, "http-url") and # link
      d($a !~ /^\s*<!--/, "comment") and # Kommentar danach
      d($b !~ /[|=][\s'\(]*$/s, "param1") and 
        # parameter oder Tabelle, auch geklammert, kursiv oder fett
      d($a !~ /^[\s'\)]*\|/s, "param2")  and
        # parameter oder Tabelle, auch geklammert, kursiv oder fett
      e($a !~ /^[^\[\]]*\][^\]]+/s, "http-label") and # vermutlich http-link
      e($a !~ /^[^<]*<\/ref>/s, "ref") and # ref-tags
      d($b !~ /<ref\s*name=[^>]*$/, "refname") and # für <ref name="....." />
      d($b !~ /\{\{PND[^\}]*$/s, "PND") and # im PND Eintrag
      d($b !~ /\{\{DOI[^\}]*$/s, "DOI-vorlage") and # im PND Eintrag
      d($b !~ /DOI=[^ \}\|]*$/s, "DOI-param") and # im PND Eintrag
      d($a !~ /^[^|\n\[]*\.(jpe?g|gif|svg|png|ogg|pdf) *[|\n]/i, "filename") and
        # Zeilen/Parameter mit Bild-Endungen am Ende
      d($a !~ /^(-?[A-Z]|-\d(?!\d*\.\d))/i, "notalone") and # nicht "freistehend"
        # filtert "er-Zweig", "-rc1"," -12", aber nicht "-12.3.1999"
      d($b !~ /(version|kernel|linux|mac os|os x)(\]\])?[ :=]*$/i, "buzz") and 
        # Buzz-Wort
      1
    );
  }

  #
  # 1.2.1999, 01.02.99
  #

  if ($mod->{shortname} eq "dk") {
    while (/(?<![.0-9])(\d{1,2})\.(?:\ |&nbsp;)*(\d{1,2})\. # Tag und Monat
            (?:\]\])?(?:\ |&nbsp;)*(?:\[\[)? # Verlinkungen
	    (\d{2}(\d{2})?)(?!\.?\d) # Jahr
	   /gx ) {
      insert_found($mod, $page->title,$`,$&,$') if _check($1, $2, $3, $`, $');
    }
  }
  
  #
  # 1.02.799
  #

  if ($mod->{shortname} eq "dkyyy") {
    while (/(?<![.0-9])(\d{1,2})\.(?:\ |&nbsp;)*(\d{1,2})\. # Tag und Monat
            (?:\]\])?(?:\ |&nbsp;)*(?:\[\[)? # Verlinkungen
	    (\d{3})(?!\.?\d) # Jahr
	   /gx ) {
      if (_check($1, $2, $3, $`, $') and
          $3 ne "000"
      ) {
        insert_found($mod, $page->title,$`,$&,$');
      }	
    }
  }

  #
  # 1999-2-1, 1999-02-01
  #
  
  if ($mod->{shortname} eq "dk") {
    while (/(?<![\-0-9])(\d{4})-(\d{1,2})-(\d{1,2})(?![\-0-9])/g) {
      if (_check($3, $2, $1, $`, $') and
          $` !~ /(CAS|DIN|EN|VDE|ISO|EC).{0,9}$/ 
      ) {
        insert_found($mod, $page->title,$`,$&,$') 
      }
    }
  }

  #
  # 01. Februar 1999,  02. Apr. 2008, 03 Jan 2009
  #
 
  if ($mod->{shortname} eq "dk") {
    while (/\b(0\d)\.?(?:\ |&nbsp;)*([\w\.]+) # Tag und Monat
            (?:\]\])? 
	    (?:(?:\ |&nbsp;)*(?:\[\[)? 
	       (\d{4})(?!\d) # Jahr
	    )? # optional
	   /gx) {
      my $monat = $monat{ucfirst lc $2};
      if (defined $monat and
          _check($1, $monat, 2000, $`, $')
      ) {
        insert_found($mod, $page->title,$`,$&,$');
      }
    }
  }

  #
  # 1. Januar 38
  #

  if ($mod->{shortname} eq "dkyy" and ! /[vn]\. (Chr\.|u\. Z)/ 
      and ! /Römische Kaiserzeit/
  ) {
    while (/\b(\d{1,2})\.\ *([\w\.]+)\ +(\d{2})(?![.:]?\d)/g) {
      if (defined $monat{$2} and
          $' !~ /^(\s*|&nbsp;)(°|%|mm|km|cm)/ and
	  $' !~ /^\?\?/ and # 19??
	  $' !~ /^,\d/ and
          $' !~ /^(\s*|&nbsp;)(\[\[)?((kilo|zenti|milli)?meter|(see)?meile
	         |uhr|mark|euro|prozent
		 |jahre|tage|stunden
		 |mann|schiffe)/ix and
          _check($1, $monat{$2}, 2000, $`, $')
      ) {
        insert_found($mod, $page->title,$`,$&,$');
      }
    }
  }

  #
  #  20er
  #
  
  if ($mod->{shortname} eq "dker" or
#      $mod->{shortname} eq "dk" or 
      0) {
    while (/(?<![\-0-9])([1-9]0er[-\ ]*Jahre)/g) {
      if ($' !~ /^.{0,30}(Jahrhundert|Jh)/) {
        $secondary_flag = $mod->{shortname} eq "dk";
        insert_found($mod, $page->title,$`,$&,$');
      }	
    }
  }

}

###########################################
sub mod_plusdagger {
###########################################
  my($mod, $page) = @_;
  $_ = ${$page->text};
  while (/[(;,] ?\+ ?(\[\[)?\d{1,2}\./) {
    insert_found($mod, $page->title,$`,$&,$');
    $_ = $';
  }
}

###########################################
# Allgemeine Routinen
###########################################

###########################################
sub insert_found {
###########################################
  my($mod, $title, $before, $match, $after) = @_;
  my $show_context = $mod->{show_context};
  my $show_info = $mod->{show_info};
 
  $found++;
  $secondary_count++ if $secondary_flag;
  $secondary_flag = 0;
  if ($show_context) {
    $context .= substr($before,length($before)-$show_context,$show_context)
        	      .$match.substr($after,0,$show_context)."\n";
  }
  $match = "<nowiki>$match</nowiki>" if $match =~ /[\[\]]/;
  if ($found <= $show_info || $show_info == -1) {
    $info .= ", $match";
  } elsif ($found == $show_info + 1 && $show_info != 0) {
    $info .= ", ...";
  }
}

###########################################
sub insert_simple {
###########################################
  my ($mod, $title, $text) = @_;
  insert_found($mod, $title, undef, $text, undef);
}

###########################################
sub output_one {
###########################################
  my ($mod, $title, $secondary) = @_;
  my $show_context = $mod->{show_context};
  my $show_info = $mod->{show_info};
  my $show_contextlines = $mod->{show_contextlines};
  chomp $context;
  print "$mod->{shortname}: ";
  print ":" if $secondary;
  print	"*[[$title]]";
  if ($info) {
    print $info;
  }
  if ($found > $show_info && $show_info >= 0) {
    print " ($found)";
  }
  print "\n";
  $| = 1;
  my $i = 0;
  foreach my $line (split /\n/, $context) {
    if ($i++ < $show_contextlines || $show_contextlines == -1) { 
      print "$mod->{shortname}: ";
      print ":" if $secondary;
      print ":<nowiki>$line</nowiki>\n"; 
    }
  }
}

__END__

=head1 NAME

wikidump-parser - Parse wikipedias xml-dump

=head1 SYNOPSIS

wikidump-parser [-h] [-i <#>] [-l <#>] [-c <#>] <xml-dump-file> <mod> [<mod> ]*

=head1 ARGUMENTS

  dk     # Datumskonvention (1.1.2000)
  dkyy   # Datumskonvention (1.1.82)
  dkyyy  # Datumskonvention (1.1.333)
  bkl    # diverses bei BKL-Seiten
  red    # Redirects

=head1 OPTIONS

  -i <#> # num. of occurences to be shown (0), -1=infty
  -l <#> # num. of context-lines to be shown (0), -1=infty;
  -l <#> # num. of context-chars to be shown before/after occurence (30)
  -h     # show help
